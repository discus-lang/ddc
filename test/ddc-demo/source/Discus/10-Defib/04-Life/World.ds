
module World
export
{       newWorld;
        getCell; setCell;
        getNeighbours;

        stepWorld;
        showWorld
}
import Cell
import Data.List
import Data.Array
import Data.Array.Store
import Data.Text
import Data.Text.Operator
where


-- | A 2D world of cells.
data World (r: Region)
 =      [ width         : Nat
        , height        : Nat
        , cells         : Array (Store r Cell) (S (Read r) Cell) ]


-- | Convert 2D coordinates to linear coordinates.
linear (world: World r) (x y: Nat): Nat
 = x + y * world.width


-- Create a new world.
newWorld (width height: Nat): S (Alloc r) (World r)
 = World [ width  = width
         , height = height
         , cells  = arrayStore_alloc {@r} (width * height) CellEmpty ]


-- | Get a single cell from the world.
getCell (world: World r) (x y: Nat): S (Read r) Cell
 | x >= world.width     = CellEmpty
 | y >= world.height    = CellEmpty
 | otherwise            = array_index world.cells (linear world x y)


-- | Set a single cell in the world.
setCell (world: World r) (x y: Nat) (c: Cell): S (Write r) Unit
 | x >= world.width     = ()
 | y >= world.height    = ()
 | otherwise            = arrayStore_write world.cells (linear world x y) c


-- | Get the neighbours of the given location.
getNeighbours
        (world: World r) (x y: Nat)
        : S (Read r) (List Cell)
 = mapS (Î»(T2 x' y') -> getCell world x' y')
 $ Cons (T2 (x - 1) (y - 1)) $ Cons (T2 x (y - 1)) $ Cons (T2 (x + 1) (y - 1))
 $ Cons (T2 (x - 1)  y)                            $ Cons (T2 (x + 1)  y)
 $ Cons (T2 (x - 1) (y + 1)) $ Cons (T2 x (y + 1)) $ Cons (T2 (x + 1) (y + 1))
 $ Nil


-- | Step a world forward in time, returning a new world.
stepWorld
        (world: World r)
        : S (Read r + Alloc r) (World r)
 =  forWorld world $ \x y
 -> stepCell
        (getCell       world x y)
        (getNeighbours world x y)


-- | Compute a new world by applying the given worker function
--   to every available location.
forWorld
        (world: World r)
        (f:     Nat -> Nat -> S (Read r) Cell)
        : S (Read r + Alloc r) (World r)
 = extend r using mutable r' in
   do
        world'  = newWorld {@r'} world.width world.height

        rec go (x y: Nat): S (Read r + Read r' + Write r' + Alloc r') Unit
                | x >= world.width   = go 0 (y + 1)
                | y >= world.height  = ()
                | otherwise
                = do    setCell world' x y (f x y)
                        go (x + 1) y

        go 0 0
        world'


-- Show a world as a text string.
showWorld (world: World r): S (Read r) Text
 = go 0 0 ""
 where
        go (x y: Nat) (acc: Text): S (Read r) Text
         | x >= world.width   = go 0 (y + 1) (acc % "\n")
         | y >= world.height  = acc
         | otherwise
         = go (x + 1) y
                $ acc % case getCell world x y of
                         CellEmpty -> ". "
                         CellAlive -> "O "

